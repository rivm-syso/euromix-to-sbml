function conc(q,vol)
  piecewise(q/vol, vol > 0, 0)
end

function metab_MM(Vmax, Km, PC, q, vol)
  vol*Vmax*conc(q, vol) / (PC*Km+conc(q, vol))
end

function metab_MA(CLH, PC, q, vol)
  CLH*conc(q, vol)/PC
end

model euromix()

  # Parameters

  ## Physiology

  ### Body size
  BM = 70             # body weight [kg]
  BSA = 190           # body surface area [cm2]

  ### Relative compartment volumes
  scVFat = 0.209      # fat volume as as fraction of total body volume [L/kg]
  scVRich = 0.105     # richly perfused tissues volume as as fraction of total body volume [L/kg]
  scVLiver = 0.024    # liver volume as as fraction of total body volume [L/kg]
  scVBlood = 0.068    # blood volume as as fraction of total body volume [L/kg]
  scVArt = 0.333      # arterial blood volume as fraction of total blood volume

  ### Flow rates
  scFBlood = 4.8      # total blood flow per unit mass [L/(kg.h)]
  scFFat = 0.085      # fraction of blood flow going to adipose tissue [-]
  scFPoor = 0.12      # fraction total blood flow going to poorly perfused tissue [-]
  scFLiver = 0.27     # fraction total blood flow going to liver [-]
  scFSkin = 0.05      # fraction total blood flow going to skin [-]

  ### Skin parameters
  fSA_exposed = 0.1   # fraction of skin surface area actually exposed [-]
  Height_sc = 0.0001  # thickness stratum corneum [dm]
  Height_vs = 0.0122  # thickness viable epidermis [dm]

  ### Lung parameters
  Falv = 2220         # alveolar ventilation rate [L/h]

  ## Biochemical parameters

  ### Partition coefficients
  PCFat = 2.53        # partition coefficient fat over blood [-]
  PCLiver = 0.923     # partition coefficient liver over blood [-]
  PCRich = 0.875      # partition coefficient poorly perfused tissue over blood [-]
  PCPoor = 0.647      # partition coefficient richly perfused tissue over blood [-]
  PCSkin_sc = 0.889   # partition coefficient viable skin over stratum corneum [-]
  PCSkin = 0.889      # partition coefficient viable skin over blood [-]
  PCAir = 1e+99       # partition coefficient blood over air [-]

  ## Absorption
  kGut = 1            # absorption rate constant gut [1/h]
  Kp_sc_vs = 0.01     # diffusion rate from stratum corneum to viable skin [dm/h]

  ### Metabolism
  Km = 0              # Michaelis-Menten constant liver [mmol]
  Michaelis = 0       # flag for Michaelis-Menten or linear metabolism [-]
  Vmax = 0            # maximum rate of metabolism in the liver [mmol/(L.h)]

  ### Excretion
  CLH = 132           # hepatic clearance rate [L/h]
  Ke = 7.5            # renal excretion rate [L/h]

  ### Others
  fub = 0.51          # fraction unbound in blood [-]

  # Assignments

  ## Physiology

  ### Body size
  f_su := Kp_sc_vs * BSA * (1 - fSA_exposed)
  f_se := Kp_sc_vs * BSA * fSA_exposed

  ### Flow rates
  FBlood := scFBlood * BM
  FFat := FBlood * scFFat
  FPoor := FBlood * scFPoor
  FLiver := FBlood * scFLiver
  FSkin := FBlood * scFSkin
  FRich := FBlood - FFat - FPoor - FLiver - FSkin
  FSkin_e := FSkin * fSA_exposed
  FSkin_u := FSkin - FSkin_e

  # Compartments
  compartment Air = 1E8
  compartment Urine
  compartment Fat := BM * scVFat
  compartment Rich := BM * scVRich
  compartment Liver := BM * scVLiver
  compartment Art := BM * scVBlood * scVArt
  compartment Ven := BM * scVBlood - Art
  compartment Skin_e := BSA * Height_vs * fSA_exposed
  compartment Skin_u := BSA * Height_vs * (1 - fSA_exposed)
  compartment Skin_sc_e := BSA * Height_sc * fSA_exposed
  compartment Skin_sc_u := BSA * Height_sc * (1 - fSA_exposed)
  compartment Poor := BM * (1 - scVFat - scVRich - scVLiver - scVBlood - 0.1) - Skin_e - Skin_u - Skin_sc_e - Skin_sc_u
  compartment Gut

  # Species
  substanceOnly species QFat in Fat = 0
  substanceOnly species QRich in Rich = 0
  substanceOnly species QPoor in Poor = 0
  substanceOnly species QLiver in Liver = 0
  substanceOnly species QMetab in Liver = 0
  substanceOnly species QGut in Gut = 0
  substanceOnly species QSkin_u in Skin_u = 0
  substanceOnly species QSkin_e in Skin_e = 0
  substanceOnly species QSkin_sc_u in Skin_sc_u = 0
  substanceOnly species QSkin_sc_e in Skin_sc_e = 0
  substanceOnly species QArt in Art = 0
  substanceOnly species QVen in Ven = 0
  substanceOnly species QExcret in Urine = 0
  substanceOnly species QAir in Air = 0

  # Model

  # Air exchange
  QArt -> QAir; Falv * (QArt/Art) / PCAir
  QAir -> QArt; FBlood * (QAir/Air)

  ## Distribution
  QArt -> QFat; FFat * (QArt/Art)
  QFat -> QVen; FFat * (QFat/Fat) / PCFat

  QArt -> QRich; FRich * (QArt/Art)
  QRich -> QVen; FRich * (QRich/Rich) / PCRich

  QArt -> QPoor; FPoor * (QArt/Art)
  QPoor -> QVen; FPoor * (QPoor/Poor) / PCPoor

  QArt -> QLiver; FLiver * (QArt/Art)
  QLiver -> QVen; FLiver * (QLiver/Liver) / PCLiver
  QGut -> QLiver; kGut * QGut

  QArt -> QSkin_u; FSkin_u * (QArt/Art)
  QSkin_u -> QVen; FSkin_u * conc(QSkin_u, Skin_u) / PCSkin
  QSkin_u -> QSkin_sc_u; f_su * conc(QSkin_u, Skin_u) / PCSkin_sc
  QSkin_sc_u -> QSkin_u; f_su * conc(QSkin_sc_u, Skin_sc_u)

  QArt -> QSkin_e; FSkin_e * (QArt/Art)
  QSkin_e -> QVen; FSkin_e * conc(QSkin_e, Skin_e) / PCSkin
  QSkin_e -> QSkin_sc_e; f_se * conc(QSkin_e, Skin_e) / PCSkin_sc
  QSkin_sc_e -> QSkin_e; f_se * conc(QSkin_sc_e, Skin_sc_e)

  QVen -> QArt; FBlood * (QVen/Ven)

  ## Metabolism
  QLiver -> QMetab; fub * piecewise(metab_MM(Vmax, Km, PCLiver, QLiver, Liver), Michaelis > 0.5, metab_MA(CLH, PCLiver, QLiver, Liver))

  ## Excretion
  QArt -> QExcret; Ke * fub * (QArt/Art)

end
