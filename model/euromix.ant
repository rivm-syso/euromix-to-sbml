function conc(q,vol)
  piecewise(q/vol, vol > 1e-5, 0)
end

function metab_MM(Vmax, Km, PC, q, vol)
  vol*Vmax*conc(q, vol) / (PC*Km+conc(q, vol))
end

function metab_MA(CLH, PC, q, vol)
  CLH*conc(q, vol)/PC
end

model euromix()

  # Parameters
  
  ## Physiology

  ### Body size
  BM = 70
  BSA = 190
  Height_sc = 0.0001
  Height_vs = 0.0122
  scVFat = 0.209
  scVRich = 0.105
  scVLiver = 0.024
  scVBlood = 0.068

  ### Flow rates
  scFBlood = 4.8
  scFFat = 0.085
  scFPoor = 0.12
  scFLiver = 0.27
  scFSkin = 0.05
  Falv = 2220

  ## Biochemical parameters

  ### Partition coefficients
  PCFat = 2.53
  PCLiver = 0.923
  PCRich = 0.875
  PCPoor = 0.647
  PCSkin_sc = 0.889
  PCSkin = 0.889

  PCAir = 1e+99

  ### Metabolism
  Km = 0
  Michaelis = 0
  Vmax = 0

  ### Others
  fub = 0.51
  fSA_exposed = 0.1
  Kp_sc_vs = 0.01

  ### Excretion
  kGut = 1
  CLH = 132
  Ke = 7.5

  # Assignments
  
  ## Physiology

  ### Body size
  const VFat := BM * scVFat
  const VRich := BM * scVRich
  const VLiver := BM * scVLiver
  const VBlood := BM * scVBlood
  const VArt := VBlood / 3.0
  const VVen := VBlood - VArt
  const VSkin_e := BSA * Height_vs * fSA_exposed
  const VSkin_u := BSA * Height_vs * (1 - fSA_exposed)
  const VSkin_sc_e := BSA * Height_sc * fSA_exposed
  const VSkin_sc_u := BSA * Height_sc * (1 - fSA_exposed)
  const VPoor := BM - VFat - VRich - VLiver - VSkin_e - VSkin_u - VSkin_sc_e - VSkin_sc_u - VBlood - 0.1 * BM

  const f_su := Kp_sc_vs * BSA * (1 - fSA_exposed)
  const f_se := Kp_sc_vs * BSA * fSA_exposed

  ### Flow rates
  const FBlood := scFBlood * BM
  const FFat := FBlood * scFFat
  const FPoor := FBlood * scFPoor
  const FLiver := FBlood * scFLiver
  const FSkin := FBlood * scFSkin
  const FRich := FBlood - FFat - FPoor - FLiver - FSkin
  const FSkin_e := FSkin * fSA_exposed
  const FSkin_u := FSkin - FSkin_e

  # Compartments and Species
  compartment Fat = VFat
  compartment Rich = VRich
  compartment pp = VPoor
  compartment liver = VLiver
  compartment gut
  compartment su = VSkin_u
  compartment se = VSkin_e
  compartment scu = VSkin_sc_u
  compartment sce = VSkin_sc_e
  compartment Art = VArt
  compartment Ven = VVen

  # Species
  substanceOnly species QFat in Fat
  substanceOnly species QRich in Rich
  substanceOnly species QPoor in pp
  substanceOnly species QLiver in liver
  substanceOnly species QMetab in liver
  substanceOnly species QGut in gut
  substanceOnly species QSkin_u in su
  substanceOnly species QSkin_e in se
  substanceOnly species QSkin_sc_u in scu
  substanceOnly species QSkin_sc_e in sce
  substanceOnly species QArt in Art
  substanceOnly species QVen in Ven
  substanceOnly species QExcret

  QFat = 0
  QPoor = 0
  QRich = 0
  QGut = 0
  QMetab = 0
  QLiver = 0
  QSkin_u = 0
  QSkin_e = 0
  QSkin_sc_u = 0
  QSkin_sc_e = 0
  QArt = 0
  QVen = 0

  # Model

  ## Absorption
  # Should either be done by events
  # or by adding boundary species

  ## Distribution
  QArt -> QFat; FFat * conc(QArt, Art)
  QFat -> QVen; FFat * conc(QFat, Fat) / PCFat

  QArt -> QRich; FRich * conc(QArt, Art)
  QRich -> QVen; FRich * conc(QRich, Rich) / PCRich

  QArt -> QPoor; FPoor * conc(QArt, Art)
  QPoor -> QVen; FPoor * conc(QPoor, pp) / PCPoor

  QArt -> QLiver; FLiver * conc(QArt, Art)
  QLiver -> QVen; FLiver * conc(QLiver, liver) / PCLiver
  QGut -> QLiver; kGut * QGut

  QArt -> QSkin_u; FSkin_u * conc(QArt, Art)
  QSkin_u -> QVen; FSkin_u * conc(QSkin_u, su) / PCSkin
  QSkin_u -> QSkin_sc_u; f_su * conc(QSkin_u, su) / PCSkin_sc
  QSkin_sc_u -> QSkin_u; f_su * conc(QSkin_sc_u, scu)

  QArt -> QSkin_e; FSkin_e * conc(QArt, Art)
  QSkin_e -> QVen; FSkin_e * conc(QSkin_e, se) / PCSkin
  QSkin_e -> QSkin_sc_e; f_se * conc(QSkin_e, se) / PCSkin_sc
  QSkin_sc_e -> QSkin_e; f_se * conc(QSkin_sc_e, sce)

  QVen -> QArt; FBlood * conc(QVen, Ven)

  ## Metabolism

  QLiver -> QMetab; fub * piecewise(metab_MM(Vmax, Km, PCLiver, QLiver, liver), Michaelis > 0.5, metab_MA(CLH, PCLiver, QLiver, liver))

  ## Excretion
  QArt -> QAir; Falv * conc(QArt, Art) / PCAir
  QArt -> QExcret; Ke * fub * conc(QArt, Art)

end
